resource "rancher2_project_role_template_binding" "cluster_project_role_template_binding" {
  for_each = {
    for item in flatten([
      for project_name, project_detail in var.projects_info : [
        for user_name, role_template_name in(project_detail.role_template_binding != null ? project_detail.role_template_binding : {}) : {
          key                = "${project_name}-${lower(replace(user_name, " ", "-"))}-prtb"
          binding_name       = "${project_name}-${lower(replace(user_name, " ", "-"))}-prtb"
          project_name       = project_name
          user_name          = user_name
          role_template_name = role_template_name
        }
      ]
    ]) : item.key => item
  }

  name             = each.value.binding_name
  project_id       = rancher2_project.cluster_project[each.value.project_name].id
  role_template_id = data.rancher2_role_template.rancher_role_template[each.value.role_template_name].id
  user_id          = data.rancher2_user.rancher_user[each.value.user_name].id
  depends_on = [
    rancher2_project.cluster_project,
    data.rancher2_role_template.rancher_role_template,
    data.rancher2_user.rancher_user
  ]
}